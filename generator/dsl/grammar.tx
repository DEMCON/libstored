Store:
	objects*=Object
;

Object:
	/\s*/-
	( Variable | Function | Scope )
	/\s*/-
;

Variable[noskipws]:
	type=InitializedType name=Name 
;

Function[noskipws]:
	'(' type=Type ')' len=Array name=Name
;

Scope[noskipws]:
	'{' objects*=Object '}' len=Array name=Name
;

InitializedType[noskipws]:
	fixed=FixedTypeArray ('=' init=Immediate ) ? | blob=BlobType
;

Type[noskipws]:
	fixed=FixedTypeArray | blob=BlobType
;

FixedTypeArray[noskipws]:
	type=FixedType len=Array
;

Array[noskipws]:
	( '['- INT ']'- ) ?
;

FixedType[noskipws]:
	'bool' |
	'int8' | 'uint8' |
	'int16' | 'uint16' |
	'int32' | 'uint32' |
	'int64' | 'uint64' |
	'float' | 'double' |
	'ptr32' | 'ptr64'
;

BlobType[noskipws]:
	type=BlobBaseType ':' size=INT ( '[' len=INT ']' ) ?
;

BlobBaseType[noskipws]:
	( 'string' | 'blob' ) 
;

Name[noskipws]:
	/\s+/-
	/([ -.0-~]\/?)*[!-.0-~]/
;

Immediate[noskipws]:
	NUMBER | BOOL
;

Comment:
	/\/\/.*$/
;

